//@version=6
indicator(title="VWAP Deluxe", overlay=true)

////////////////////////////////////////////////////////////////////////////////////////////////////
// Version History
////////////////////////////////////////////////////////////////////////////////////////////////////
// v1.1.1
// - Improved inputs display
// - Improved plot naming
// v1.1.0
// - Improved settings
// v1.0.0
// - Initial release
////////////////////////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////////////////////////
// Enums
////////////////////////////////////////////////////////////////////////////////////////////////////

enum StandardDeviations
    none = "None"
    one = "One"
    two = "Two"
    three = "Three"

enum VWAPLevel
    upper_band3 = "Upper Band #3"
    upper_band2 = "Upper Band #2"
    upper_band1 = "Upper Band #1"
    vwap = "VWAP"
    lower_band1 = "Lower Band #1"
    lower_band2 = "Lower Band #2"
    lower_band3 = "Lower Band #3"


////////////////////////////////////////////////////////////////////////////////////////////////////
// Constants
////////////////////////////////////////////////////////////////////////////////////////////////////

var VWAP_SETTINGS_GROUP = "VWAP Settings"
var BANDS_SETTINGS_GROUP = "Bands Settings"
var DISPLAY_GROUP = "Display"
var BARS_GROUP = "Bars"
var ALERTS_GROUP = "Alerts"
var DEVELOPER_GROUP = "DEVELOPER"


////////////////////////////////////////////////////////////////////////////////////////////////////
// Inputs
////////////////////////////////////////////////////////////////////////////////////////////////////

hideonDWM = input(false, title="Hide VWAP on 1D or Above", group=VWAP_SETTINGS_GROUP, display = display.data_window)
var anchor = input.string(defval = "Session", title="Anchor Period",
 options=["Session", "Week", "Month", "Quarter", "Year", "Decade", "Century", "Earnings", "Dividends", "Splits"], group=VWAP_SETTINGS_GROUP)
src = input(title = "Source", defval = ohlc4, group=VWAP_SETTINGS_GROUP, display = display.data_window)
offset = input.int(0, title="Offset", group=VWAP_SETTINGS_GROUP, minval=0, display = display.data_window)

StandardDeviations i_num_std_deviations = input.enum(StandardDeviations.three, "Standard Deviations", group = BANDS_SETTINGS_GROUP)
var bool showBand_1 = i_num_std_deviations != StandardDeviations.none
var bool showBand_2 = i_num_std_deviations == StandardDeviations.two or i_num_std_deviations == StandardDeviations.three
var bool showBand_3 = i_num_std_deviations == StandardDeviations.three

i_std_dev_1_mult = input.float(1.0, title = "Std Dev 1 Multiplier", group = BANDS_SETTINGS_GROUP, step = 0.5, minval=0, active = showBand_1)
i_std_dev_2_mult = input.float(2.0, title = "Std Dev 2 Multiplier", group = BANDS_SETTINGS_GROUP, step = 0.5, minval=0, active = showBand_2)
i_std_dev_3_mult = input.float(3.0, title = "Std Dev 3 Multiplier", group = BANDS_SETTINGS_GROUP, step = 0.5, minval=0, active = showBand_3)

i_stddev1_upper_area_color = input.color(color.new(color.blue, 70), "Std Dev 1 Upper Area", group = DISPLAY_GROUP, active = showBand_1)
i_stddev1_lower_area_color = input.color(color.new(color.blue, 70), "Std Dev 1 Lower Area", group = DISPLAY_GROUP, active = showBand_1)

i_stddev2_upper_area_color = input.color(color.new(color.blue, 80), "Std Dev 2 Upper Area", group = DISPLAY_GROUP, active = showBand_2)
i_stddev2_lower_area_color = input.color(color.new(color.blue, 80), "Std Dev 2 Lower Area", group = DISPLAY_GROUP, active = showBand_2)

i_stddev3_upper_area_color = input.color(color.new(color.blue, 90), "Std Dev 3 Upper Area", group = DISPLAY_GROUP, active = showBand_3)
i_stddev3_lower_area_color = input.color(color.new(color.blue, 90), "Std Dev 3 Lower Area", group = DISPLAY_GROUP, active = showBand_3)

i_color_vwap_by_price = input.bool(false, "Color VWAP by price", inline = "Color VWAP by price", group = DISPLAY_GROUP)
i_above_price_vwap_color = input.color(color.red, "", inline = "Color VWAP by price", group = DISPLAY_GROUP)
i_below_price_vwap_color = input.color(color.green, "", inline = "Color VWAP by price", group = DISPLAY_GROUP)

i_show_bars_in_balance_region = input.bool(false, "Highlight bars in balance region", "Highlight the bars closing inside the Std Deviation 1 bands", inline = "Balance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_balance_region_bullish_color = input.color(#b8b8b8, "", inline = "Balance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_balance_region_bearish_color = input.color(#636363, "", inline = "Balance Region", group = BARS_GROUP, active = showBand_1)

i_show_bars_in_bullish_imbalance_region = input.bool(false, "Highlight bars in bullish imbalance region", "Highlight the bars closing above the Std Deviation 1 Upper band", inline = "Bullish Imbalance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_bullish_imbalance_region_bullish_color = input.color(#4caf50, "", inline = "Bullish Imbalance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_bullish_imbalance_region_bearish_color = input.color(#1b5e20, "", inline = "Bullish Imbalance Region", group = BARS_GROUP, active = showBand_1)

i_show_bars_in_bearish_imbalance_region = input.bool(false, "Highlight bars in bearish imbalance region", "Highlight the bars closing below the Std Deviation 1 Lower band", inline = "Bearish Imbalance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_bearish_imbalance_region_bullish_color = input.color(#f23645, "", inline = "Bearish Imbalance Region", group = BARS_GROUP, active = showBand_1)
i_bars_in_bearish_imbalance_region_bearish_color = input.color(#801922, "", inline = "Bearish Imbalance Region", group = BARS_GROUP, active = showBand_1)

i_show_interactions = input.bool(false, "Highlight bar/VWAP interactions", "Highlight the bars touching VWAP or any Std Deviation band", inline = "Interactions", group = BARS_GROUP)
i_interaction_bullish_color = input.color(#ffeb3b, "", inline = "Interactions", group = BARS_GROUP)
i_interaction_bearish_color = input.color(#f57f17, "", inline = "Interactions", group = BARS_GROUP)

i_use_alert_window = input.bool(false, "Use alert window", inline = "Alert Window", group = ALERTS_GROUP)
i_alert_window = input.session("0800-1500", "", inline = "Alert Window", group = ALERTS_GROUP, active = i_use_alert_window, display = display.none)
i_alert_on_vwap = input.bool(false, "Alerts on VWAP interactions", group = ALERTS_GROUP)
i_alert_on_band1 = input.bool(false, "Alerts on Std Dev 1 interactions", group = ALERTS_GROUP, active = showBand_1)
i_alert_on_band2 = input.bool(false, "Alerts on Std Dev 2 interactions", group = ALERTS_GROUP, active = showBand_2)
i_alert_on_band3 = input.bool(false, "Alerts on Std Dev 3 interactions", group = ALERTS_GROUP, active = showBand_3)

enum VersionEnum
    version = "v1.1.1"
    
VersionEnum i_version = input.enum(VersionEnum.version, "Version", display = display.none, group = DEVELOPER_GROUP)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Functions
////////////////////////////////////////////////////////////////////////////////////////////////////

is_bullish(int bars_ago) =>
    close[bars_ago] >= open[bars_ago]

is_bearish(int bars_ago) =>
    close[bars_ago] < open[bars_ago]


////////////////////////////////////////////////////////////////////////////////////////////////////
// Calculations
////////////////////////////////////////////////////////////////////////////////////////////////////

cumVolume = ta.cum(volume)
if barstate.islast and cumVolume == 0
    runtime.error("No volume is provided by the data vendor.")

new_earnings = request.earnings(syminfo.tickerid, earnings.actual, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_dividends = request.dividends(syminfo.tickerid, dividends.gross, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_split = request.splits(syminfo.tickerid, splits.denominator, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)

isNewPeriod = switch anchor
    "Earnings"  => not na(new_earnings)
    "Dividends" => not na(new_dividends)
    "Splits"    => not na(new_split)
    "Session"   => timeframe.change("D")
    "Week"      => timeframe.change("W")
    "Month"     => timeframe.change("M")
    "Quarter"   => timeframe.change("3M")
    "Year"      => timeframe.change("12M")
    "Decade"    => timeframe.change("12M") and year % 10 == 0
    "Century"   => timeframe.change("12M") and year % 100 == 0
    => false

isEsdAnchor = anchor == "Earnings" or anchor == "Dividends" or anchor == "Splits"
if na(src[1]) and not isEsdAnchor
    isNewPeriod := true

float vwapValue = na
float upperBandValue1 = na
float lowerBandValue1 = na
float upperBandValue2 = na
float lowerBandValue2 = na
float upperBandValue3 = na
float lowerBandValue3 = na

if not (hideonDWM and timeframe.isdwm)
    [_vwap, _stdevUpper, _] = ta.vwap(src, isNewPeriod, 1)
    vwapValue := _vwap
    stdevAbs = _stdevUpper - _vwap
    bandBasis = stdevAbs
    upperBandValue1 := _vwap + bandBasis * i_std_dev_1_mult
    lowerBandValue1 := _vwap - bandBasis * i_std_dev_1_mult
    upperBandValue2 := _vwap + bandBasis * i_std_dev_2_mult
    lowerBandValue2 := _vwap - bandBasis * i_std_dev_2_mult
    upperBandValue3 := _vwap + bandBasis * i_std_dev_3_mult
    lowerBandValue3 := _vwap - bandBasis * i_std_dev_3_mult


////////////////////////////////////////////////////////////////////////////////////////////////////
// Plotting
////////////////////////////////////////////////////////////////////////////////////////////////////

color vwap_color = (not i_color_vwap_by_price) ? color.aqua : (vwapValue > close ? i_above_price_vwap_color : i_below_price_vwap_color)
vwap_plot = plot(vwapValue, title = "VWAP", color = vwap_color, linewidth = 3, offset = offset)

upperBand_1_plot = plot(upperBandValue1, title="Std Dev 1 Upper", color = color.blue, offset = offset, display = showBand_1 ? display.all : display.none, editable = showBand_1)
lowerBand_1_plot = plot(lowerBandValue1, title="Std Dev 1 Lower", color = color.blue, offset = offset, display = showBand_1 ? display.all : display.none, editable = showBand_1)

upperBand_2_plot = plot(upperBandValue2, title="Std Dev 2 Upper", color = color.blue, offset = offset, display = showBand_2 ? display.all : display.none, editable = showBand_2)
lowerBand_2_plot = plot(lowerBandValue2, title="Std Dev 2 Lower", color = color.blue, offset = offset, display = showBand_2 ? display.all : display.none, editable = showBand_2)

upperBand_3_plot = plot(upperBandValue3, title="Std Dev 3 Upper", color = color.blue, offset = offset, display = showBand_3 ? display.all : display.none, editable = showBand_3)
lowerBand_3_plot = plot(lowerBandValue3, title="Std Dev 3 Lower", color = color.blue, offset = offset, display = showBand_3 ? display.all : display.none, editable = showBand_3)

fill(upperBand_1_plot, vwap_plot, title="Std Dev 1 Upper Area", color = i_stddev1_upper_area_color, display = showBand_1 ? display.all : display.none, editable = showBand_1)
fill(lowerBand_1_plot, vwap_plot, title="Std Dev 1 Lower Area", color = i_stddev1_lower_area_color, display = showBand_1 ? display.all : display.none, editable = showBand_1)

fill(upperBand_2_plot, upperBand_1_plot, title="Std Dev 2 Upper Area", color = i_stddev2_upper_area_color, display = showBand_1 and showBand_2 ? display.all : display.none, editable = showBand_1 and showBand_2)
fill(lowerBand_2_plot, lowerBand_1_plot, title="Std Dev 2 Lower Area", color = i_stddev2_lower_area_color, display = showBand_1 and showBand_2 ? display.all : display.none, editable = showBand_1 and showBand_2)

fill(upperBand_3_plot, upperBand_2_plot, title="Std Dev 3 Upper Area", color = i_stddev3_upper_area_color, display = showBand_2 and showBand_3 ? display.all : display.none, editable = showBand_2 and showBand_3)
fill(lowerBand_3_plot, lowerBand_2_plot, title="Std Dev 3 Lower Area", color = i_stddev3_lower_area_color, display = showBand_2 and showBand_3 ? display.all : display.none, editable = showBand_2 and showBand_3)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Bar Colors
////////////////////////////////////////////////////////////////////////////////////////////////////

bar_in_balance_region = close <= upperBandValue1 and close >= lowerBandValue1
bar_in_balance_region_color = (i_show_bars_in_balance_region and bar_in_balance_region) ? (is_bullish(0) ? i_bars_in_balance_region_bullish_color : i_bars_in_balance_region_bearish_color) : na

bar_in_bullish_imbalance_region = close > upperBandValue1
bar_in_bullish_imbalance_region_color = (i_show_bars_in_bullish_imbalance_region and bar_in_bullish_imbalance_region) ? (is_bullish(0) ? i_bars_in_bullish_imbalance_region_bullish_color : i_bars_in_bullish_imbalance_region_bearish_color) : na

bar_in_bearish_imbalance_region = close < lowerBandValue1
bar_in_bearish_imbalance_region_color = (i_show_bars_in_bearish_imbalance_region and bar_in_bearish_imbalance_region) ? (is_bullish(0) ? i_bars_in_bearish_imbalance_region_bullish_color : i_bars_in_bearish_imbalance_region_bearish_color) : na

touching_vwap = vwapValue <= high and vwapValue >= low

touching_band1_upper = showBand_1 and (upperBandValue1 <= high and upperBandValue1 >= low)
touching_band1_lower = showBand_1 and (lowerBandValue1 <= high and lowerBandValue1 >= low)
touching_band1 = showBand_1 and (touching_band1_upper or touching_band1_lower)

touching_band2_upper = showBand_2 and (upperBandValue2 <= high and upperBandValue2 >= low)
touching_band2_lower = showBand_2 and (lowerBandValue2 <= high and lowerBandValue2 >= low)
touching_band2 = showBand_2 and (touching_band2_upper or touching_band2_lower)

touching_band3_upper = showBand_3 and (upperBandValue3 <= high and upperBandValue3 >= low)
touching_band3_lower = showBand_3 and (lowerBandValue3 <= high and lowerBandValue3 >= low)
touching_band3 = showBand_3 and (touching_band3_upper or touching_band3_lower)

interaction = i_show_interactions and (touching_vwap or touching_band1 or touching_band2 or touching_band3)
interaction_bar_color = (i_show_interactions and interaction) ? (is_bullish(0) ? i_interaction_bullish_color : i_interaction_bearish_color) : na

bar_color = (i_show_interactions and interaction) ? interaction_bar_color : 
   (i_show_bars_in_bullish_imbalance_region and bar_in_bullish_imbalance_region ? bar_in_bullish_imbalance_region_color :
     (i_show_bars_in_bearish_imbalance_region and bar_in_bearish_imbalance_region ? bar_in_bearish_imbalance_region_color :
       (i_show_bars_in_balance_region and bar_in_balance_region ? bar_in_balance_region_color : na)))

barcolor(bar_color)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Alerts
////////////////////////////////////////////////////////////////////////////////////////////////////

varip VWAPLevel last_alert_level = na

if (not i_use_alert_window) or (i_use_alert_window and not na(time("", i_alert_window)))
    if i_alert_on_vwap and touching_vwap and (na(last_alert_level) or last_alert_level != VWAPLevel.vwap)
        alert(syminfo.ticker + " touching VWAP")
        last_alert_level := VWAPLevel.vwap

    if i_alert_on_band1 and touching_band1_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band1)
        alert(syminfo.ticker + " touching Std Dev 1 Upper")
        last_alert_level := VWAPLevel.upper_band1
    if i_alert_on_band1 and touching_band1_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band1)
        alert(syminfo.ticker + " touching Std Dev 1 Lower")
        last_alert_level := VWAPLevel.lower_band1

    if i_alert_on_band2 and touching_band2_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band2)
        alert(syminfo.ticker + " touching Std Dev 2 Upper")
        last_alert_level := VWAPLevel.upper_band2
    if i_alert_on_band2 and touching_band2_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band2)
        alert(syminfo.ticker + " touching Std Dev 2 Lower")
        last_alert_level := VWAPLevel.lower_band2

    if i_alert_on_band3 and touching_band3_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band3)
        alert(syminfo.ticker + " touching Std Dev 3 Upper")
        last_alert_level := VWAPLevel.upper_band3
    if i_alert_on_band3 and touching_band3_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band3)
        alert(syminfo.ticker + " touching Std Dev 3 Lower")
        last_alert_level := VWAPLevel.lower_band3
