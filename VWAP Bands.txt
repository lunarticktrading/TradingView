//@version=6
indicator(title="VWAP Bands", overlay=true)

////////////////////////////////////////////////////////////////////////////////////////////////////
// Enums
////////////////////////////////////////////////////////////////////////////////////////////////////

enum VWAPLevel
    upper_band3 = "Upper Band #3"
    upper_band2 = "Upper Band #2"
    upper_band1 = "Upper Band #1"
    vwap = "VWAP"
    lower_band1 = "Lower Band #1"
    lower_band2 = "Lower Band #2"
    lower_band3 = "Lower Band #3"


////////////////////////////////////////////////////////////////////////////////////////////////////
// Inputs
////////////////////////////////////////////////////////////////////////////////////////////////////

hideonDWM = input(false, title="Hide VWAP on 1D or Above", group="VWAP Settings", display = display.data_window)
var anchor = input.string(defval = "Session", title="Anchor Period",
 options=["Session", "Week", "Month", "Quarter", "Year", "Decade", "Century", "Earnings", "Dividends", "Splits"], group="VWAP Settings")
src = input(title = "Source", defval = ohlc4, group="VWAP Settings", display = display.data_window)
offset = input.int(0, title="Offset", group="VWAP Settings", minval=0, display = display.data_window)

BANDS_GROUP = "Bands Settings"
CALC_MODE_TOOLTIP = "Determines the units used to calculate the distance of the bands. When 'Percentage' is selected, a multiplier of 1 means 1%."
calcModeInput = input.string("Standard Deviation", "Bands Calculation Mode", options = ["Standard Deviation", "Percentage"], group = BANDS_GROUP, tooltip = CALC_MODE_TOOLTIP, display = display.data_window)
showBand_1 = input(true, title = "", group = BANDS_GROUP, inline = "band_1", display = display.data_window)
bandMult_1 = input.float(1.0, title = "Bands Multiplier #1", group = BANDS_GROUP, inline = "band_1", step = 0.5, minval=0, display = display.data_window, active = showBand_1)
showBand_2 = input(false, title = "", group = BANDS_GROUP, inline = "band_2", display = display.data_window)
bandMult_2 = input.float(2.0, title = "Bands Multiplier #2", group = BANDS_GROUP, inline = "band_2", step = 0.5, minval=0, display = display.data_window, active = showBand_2)
showBand_3 = input(false, title = "", group = BANDS_GROUP, inline = "band_3", display = display.data_window)
bandMult_3 = input.float(3.0, title = "Bands Multiplier #3", group = BANDS_GROUP, inline = "band_3", step = 0.5, minval=0, display = display.data_window, active = showBand_3)

i_show_bars_in_balance_region = input.bool(false, "Highlight bars in balance region", "Highlights bars in between the upper and lower levels of VWAP band #1", inline = "Balance Region", group = "Display")
i_bars_in_balance_region_bullish_color = input.color(#b8b8b8, "", inline = "Balance Region", group = "Display")
i_bars_in_balance_region_bearish_color = input.color(#636363, "", inline = "Balance Region", group = "Display")

i_show_bars_in_bullish_imbalance_region = input.bool(false, "Highlight bars in bullish imbalance region", "Highlights bars above the upper level of VWAP band #1", inline = "Bullish Imbalance Region", group = "Display")
i_bars_in_bullish_imbalance_region_bullish_color = input.color(#4caf50, "", inline = "Bullish Imbalance Region", group = "Display")
i_bars_in_bullish_imbalance_region_bearish_color = input.color(#1b5e20, "", inline = "Bullish Imbalance Region", group = "Display")

i_show_bars_in_bearish_imbalance_region = input.bool(false, "Highlight bars in bearish imbalance region", "Highlights bars below the lower level of VWAP band #1", inline = "Bearish Imbalance Region", group = "Display")
i_bars_in_bearish_imbalance_region_bullish_color = input.color(#f23645, "", inline = "Bearish Imbalance Region", group = "Display")
i_bars_in_bearish_imbalance_region_bearish_color = input.color(#801922, "", inline = "Bearish Imbalance Region", group = "Display")

i_show_interactions = input.bool(false, "Highlight VWAP interactions", "Highlights bars which touch VWAP or its bands", inline = "Interactions", group = "Display")
i_interaction_bullish_color = input.color(#ffeb3b, "", inline = "Interactions", group = "Display")
i_interaction_bearish_color = input.color(#f57f17, "", inline = "Interactions", group = "Display")

i_use_alert_window = input.bool(false, "Use alert window", inline = "Alert Window", group = "Alerts")
i_alert_window = input.session("0800-1500", "", inline = "Alert Window", group = "Alerts", active = i_use_alert_window)
i_alert_on_vwap = input.bool(false, "Alerts on VWAP interactions", group = "Alerts")
i_alert_on_band1 = input.bool(false, "Alerts on VWAP band #1 interactions", group = "Alerts", active = showBand_1)
i_alert_on_band2 = input.bool(false, "Alerts on VWAP band #2 interactions", group = "Alerts", active = showBand_2)
i_alert_on_band3 = input.bool(false, "Alerts on VWAP band #3 interactions", group = "Alerts", active = showBand_3)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Functions
////////////////////////////////////////////////////////////////////////////////////////////////////

is_bullish(int bars_ago) =>
    close[bars_ago] >= open[bars_ago]

is_bearish(int bars_ago) =>
    close[bars_ago] < open[bars_ago]


////////////////////////////////////////////////////////////////////////////////////////////////////
// Calculations
////////////////////////////////////////////////////////////////////////////////////////////////////

cumVolume = ta.cum(volume)
if barstate.islast and cumVolume == 0
    runtime.error("No volume is provided by the data vendor.")

new_earnings = request.earnings(syminfo.tickerid, earnings.actual, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_dividends = request.dividends(syminfo.tickerid, dividends.gross, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_split = request.splits(syminfo.tickerid, splits.denominator, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)

isNewPeriod = switch anchor
    "Earnings"  => not na(new_earnings)
    "Dividends" => not na(new_dividends)
    "Splits"    => not na(new_split)
    "Session"   => timeframe.change("D")
    "Week"      => timeframe.change("W")
    "Month"     => timeframe.change("M")
    "Quarter"   => timeframe.change("3M")
    "Year"      => timeframe.change("12M")
    "Decade"    => timeframe.change("12M") and year % 10 == 0
    "Century"   => timeframe.change("12M") and year % 100 == 0
    => false

isEsdAnchor = anchor == "Earnings" or anchor == "Dividends" or anchor == "Splits"
if na(src[1]) and not isEsdAnchor
    isNewPeriod := true

float vwapValue = na
float upperBandValue1 = na
float lowerBandValue1 = na
float upperBandValue2 = na
float lowerBandValue2 = na
float upperBandValue3 = na
float lowerBandValue3 = na

if not (hideonDWM and timeframe.isdwm)
    [_vwap, _stdevUpper, _] = ta.vwap(src, isNewPeriod, 1)
    vwapValue := _vwap
    stdevAbs = _stdevUpper - _vwap
    bandBasis = calcModeInput == "Standard Deviation" ? stdevAbs : _vwap * 0.01
    upperBandValue1 := _vwap + bandBasis * bandMult_1
    lowerBandValue1 := _vwap - bandBasis * bandMult_1
    upperBandValue2 := _vwap + bandBasis * bandMult_2
    lowerBandValue2 := _vwap - bandBasis * bandMult_2
    upperBandValue3 := _vwap + bandBasis * bandMult_3
    lowerBandValue3 := _vwap - bandBasis * bandMult_3


////////////////////////////////////////////////////////////////////////////////////////////////////
// Plotting
////////////////////////////////////////////////////////////////////////////////////////////////////

plot(vwapValue, title = "VWAP", color = color.orange, linewidth = 3, offset = offset)

upperBand_1 = plot(upperBandValue1, title="Upper Band #1", color = color.blue, offset = offset, display = showBand_1 ? display.all : display.none, editable = showBand_1)
lowerBand_1 = plot(lowerBandValue1, title="Lower Band #1", color = color.blue, offset = offset, display = showBand_1 ? display.all : display.none, editable = showBand_1)
fill(upperBand_1, lowerBand_1,      title="Bands Fill #1", color = color.new(color.blue, 93),   display = showBand_1 ? display.all : display.none, editable = showBand_1)

upperBand_2 = plot(upperBandValue2, title="Upper Band #2", color = color.teal, offset = offset, display = showBand_2 ? display.all : display.none, editable = showBand_2)
lowerBand_2 = plot(lowerBandValue2, title="Lower Band #2", color = color.teal, offset = offset, display = showBand_2 ? display.all : display.none, editable = showBand_2)
fill(upperBand_2, lowerBand_2,      title="Bands Fill #2", color = color.new(color.teal, 93),   display = showBand_2 ? display.all : display.none, editable = showBand_2)

upperBand_3 = plot(upperBandValue3, title="Upper Band #3", color = color.olive, offset = offset, display = showBand_3 ? display.all : display.none, editable = showBand_3)
lowerBand_3 = plot(lowerBandValue3, title="Lower Band #3", color = color.olive, offset = offset, display = showBand_3 ? display.all : display.none, editable = showBand_3)
fill(upperBand_3, lowerBand_3,      title="Bands Fill #3", color = color.new(color.olive, 93),   display = showBand_3 ? display.all : display.none, editable = showBand_3)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Bar Colors
////////////////////////////////////////////////////////////////////////////////////////////////////

bar_in_balance_region = close <= upperBandValue1 and close >= lowerBandValue1
bar_in_balance_region_color = (i_show_bars_in_balance_region and bar_in_balance_region) ? (is_bullish(0) ? i_bars_in_balance_region_bullish_color : i_bars_in_balance_region_bearish_color) : na

bar_in_bullish_imbalance_region = close > upperBandValue1
bar_in_bullish_imbalance_region_color = (i_show_bars_in_bullish_imbalance_region and bar_in_bullish_imbalance_region) ? (is_bullish(0) ? i_bars_in_bullish_imbalance_region_bullish_color : i_bars_in_bullish_imbalance_region_bearish_color) : na

bar_in_bearish_imbalance_region = close < lowerBandValue1
bar_in_bearish_imbalance_region_color = (i_show_bars_in_bearish_imbalance_region and bar_in_bearish_imbalance_region) ? (is_bullish(0) ? i_bars_in_bearish_imbalance_region_bullish_color : i_bars_in_bearish_imbalance_region_bearish_color) : na

touching_vwap = vwapValue <= high and vwapValue >= low

touching_band1_upper = showBand_1 and (upperBandValue1 <= high and upperBandValue1 >= low)
touching_band1_lower = showBand_1 and (lowerBandValue1 <= high and lowerBandValue1 >= low)
touching_band1 = showBand_1 and (touching_band1_upper or touching_band1_lower)

touching_band2_upper = showBand_2 and (upperBandValue2 <= high and upperBandValue2 >= low)
touching_band2_lower = showBand_2 and (lowerBandValue2 <= high and lowerBandValue2 >= low)
touching_band2 = showBand_2 and (touching_band2_upper or touching_band2_lower)

touching_band3_upper = showBand_3 and (upperBandValue3 <= high and upperBandValue3 >= low)
touching_band3_lower = showBand_3 and (lowerBandValue3 <= high and lowerBandValue3 >= low)
touching_band3 = showBand_3 and (touching_band3_upper or touching_band3_lower)

interaction = i_show_interactions and (touching_vwap or touching_band1 or touching_band2 or touching_band3)
interaction_bar_color = (i_show_interactions and interaction) ? (is_bullish(0) ? i_interaction_bullish_color : i_interaction_bearish_color) : na

bar_color = (i_show_interactions and interaction) ? interaction_bar_color : 
   (i_show_bars_in_bullish_imbalance_region and bar_in_bullish_imbalance_region ? bar_in_bullish_imbalance_region_color :
     (i_show_bars_in_bearish_imbalance_region and bar_in_bearish_imbalance_region ? bar_in_bearish_imbalance_region_color :
       (i_show_bars_in_balance_region and bar_in_balance_region ? bar_in_balance_region_color : na)))

barcolor(bar_color)


////////////////////////////////////////////////////////////////////////////////////////////////////
// Alerts
////////////////////////////////////////////////////////////////////////////////////////////////////

varip VWAPLevel last_alert_level = na

if (not i_use_alert_window) or (i_use_alert_window and not na(time("", i_alert_window)))
    if i_alert_on_vwap and touching_vwap and (na(last_alert_level) or last_alert_level != VWAPLevel.vwap)
        alert(syminfo.ticker + " touching VWAP")
        last_alert_level := VWAPLevel.vwap

    if i_alert_on_band1 and touching_band1_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band1)
        alert(syminfo.ticker + " touching upper VWAP band #1")
        last_alert_level := VWAPLevel.upper_band1
    if i_alert_on_band1 and touching_band1_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band1)
        alert(syminfo.ticker + " touching lower VWAP band #1")
        last_alert_level := VWAPLevel.lower_band1

    if i_alert_on_band2 and touching_band2_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band2)
        alert(syminfo.ticker + " touching upper VWAP band #2")
        last_alert_level := VWAPLevel.upper_band2
    if i_alert_on_band2 and touching_band2_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band2)
        alert(syminfo.ticker + " touching lower VWAP band #2")
        last_alert_level := VWAPLevel.lower_band2

    if i_alert_on_band3 and touching_band3_upper and (na(last_alert_level) or last_alert_level != VWAPLevel.upper_band3)
        alert(syminfo.ticker + " touching upper VWAP band #3")
        last_alert_level := VWAPLevel.upper_band3
    if i_alert_on_band3 and touching_band3_lower and (na(last_alert_level) or last_alert_level != VWAPLevel.lower_band3)
        alert(syminfo.ticker + " touching lower VWAP band #3")
        last_alert_level := VWAPLevel.lower_band3
